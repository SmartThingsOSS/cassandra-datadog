buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
	}
}

group 'smartthings'
version = rootProject.file('version.txt').text.trim()

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply from: 'gradle/publishing.gradle'

def jvmEncoding = java.nio.charset.Charset.defaultCharset().name()
if (jvmEncoding != "UTF-8") {
	throw new IllegalStateException("Build environment must be UTF-8 (it is: $jvmEncoding) - add '-Dfile.encoding=UTF-8' to the GRADLE_OPTS environment variable ")
}

if (!JavaVersion.current().java6Compatible) {
	throw new IllegalStateException("Must be built with Java 6 or higher")
}

task wrapper(type: Wrapper) {
	gradleVersion = "2.11"
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
	jcenter()
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

dependencies {
  // These match cassandra 2.1.13 and do not need to be put in class path
  compile 'com.yammer.metrics:metrics-core:2.2.0'
  compile 'com.google.guava:guava:16.0'
  compile 'org.codehaus.jackson:jackson-core-asl:1.9.2'
  compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
  compile 'org.slf4j:slf4j-api:1.7.2'
  compile 'org.yaml:snakeyaml:1.11'

  // test dependencies
  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testRuntime 'cglib:cglib-nodep:2.2.2'
  testRuntime 'org.objenesis:objenesis:1.2'
}
